plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'maven-publish'
}

group = 'com.github.JulianSanti28'
version = '0.0.6'
description = 'Monitoring Starter Library - Auto-exposes /health and /metrics endpoints'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
	withSourcesJar()
	withJavadocJar()
}

// Repositorios para dependencias
repositories {
	mavenCentral()
}

// Dependencias
dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web:3.2.2'
}

// Configuración de tests
tasks.named('test') {
	useJUnitPlatform()
}

// Evitar que Spring Boot cree un JAR ejecutable
tasks.named('bootJar') {
	enabled = false
}

// Forzar nombre correcto del JAR principal
tasks.named('jar') {
	enabled = true
	archiveBaseName.set('monitoring-tool-commons-library')
	archiveVersion.set(project.version)
}

// Configuración de publicación Maven
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			groupId = project.group
			artifactId = 'monitoring-tool-commons-library'
			version = project.version
			pom {
				name.set('Monitoring Tool Commons Library')
				description.set('Starter library exposing health and metrics endpoints')
			}
		}
	}
}

tasks.named('jar') {
	archiveBaseName.set('monitoring-tool-commons-library')
	archiveVersion.set(project.version)
}



