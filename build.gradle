plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'maven-publish'
}

group = 'com.github.JulianSanti28'
version = '0.0.8'
description = 'Monitoring Starter Library - Auto-exposes /health and /metrics endpoints'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
	withSourcesJar()
	withJavadocJar()
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web:3.2.2'
}

tasks.named('test') {
	useJUnitPlatform()
}

bootJar {
	enabled = false
}

jar {
	enabled = true
	archiveFileName = "${project.name}-${project.version}.jar"  // <-- FORZAR NOMBRE
}

// === PublicaciÃ³n Maven ===
publishing {
	publications {
		mavenJava(MavenPublication) {
			// Usar el JAR generado por la tarea 'jar'
			artifact jar

			groupId = project.group
			artifactId = project.name
			version = project.version

			pom {
				name = 'Monitoring Tool Commons Library'
				description = 'Starter library exposing health and metrics endpoints'
				url = 'https://github.com/JulianSanti28/monitoring-tool-commons-library'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}
				developers {
					developer {
						id = 'JulianSanti28'
						name = 'Julian Santiago Martinez Trullo'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/JulianSanti28/monitoring-tool-commons-library.git'
					developerConnection = 'scm:git:ssh://github.com:JulianSanti28/monitoring-tool-commons-library.git'
					url = 'https://github.com/JulianSanti28/monitoring-tool-commons-library'
				}
			}
		}
	}
}